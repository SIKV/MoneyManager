plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dev.flutter.flutter-gradle-plugin'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
def keystoreFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null

def firebaseProperties = new Properties()
def firebasePropertiesFile = rootProject.file('firebase.properties')
if (firebasePropertiesFile.exists()) {
    firebaseProperties.load(new FileInputStream(firebasePropertiesFile))
}

android {
    namespace 'sikv.moneymanager'

    compileSdkVersion flutter.compileSdkVersion
    ndkVersion '28.2.13676358'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions 'default'

    defaultConfig {
        applicationId 'sikv.moneymanager'
        minSdkVersion flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion

        resValue 'string', 'google_app_id', firebaseProperties['googleAppId']
                ? firebaseProperties['googleAppId'] : ''
        resValue 'string', 'google_api_key', firebaseProperties['googleApiKey']
                ? firebaseProperties['googleApiKey'] : ''
        resValue 'string', 'gcm_defaultSenderId', firebaseProperties['gcmDefaultSenderId']
                ? firebaseProperties['gcmDefaultSenderId'] : ''
        resValue 'string', 'project_id', firebaseProperties['projectId']
                ? firebaseProperties['projectId'] : ''
    }

    signingConfigs {
        if (keystoreFile?.exists()) {
            betaRelease {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile keystoreFile
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    productFlavors {
        prod {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            versionCode 1
            versionName ''
        }
        beta {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            applicationIdSuffix '.beta'
            versionCode 2
            versionName '2025.2-beta'
            if (keystoreFile?.exists()) {
                buildTypes.release.signingConfig signingConfigs.betaRelease
            }
        }
        dev {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            applicationIdSuffix '.dev'
            versionCode 2
            versionName '2025.2-dev'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (project.hasProperty('qa')) {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
}
