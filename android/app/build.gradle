plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dev.flutter.flutter-gradle-plugin'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def firebaseProperties = new Properties()
def firebasePropertiesFile = rootProject.file('firebase.properties')
if (firebasePropertiesFile.exists()) {
    firebaseProperties.load(new FileInputStream(firebasePropertiesFile))
}

android {
    namespace 'sikv.moneymanager'

    compileSdkVersion flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions 'default'

    defaultConfig {
        applicationId 'sikv.moneymanager'
        minSdkVersion 23 // flutter.minSdkVersion
        targetSdkVersion flutter.targetSdkVersion

        resValue 'string', 'google_app_id', firebaseProperties['googleAppId']
                ? firebaseProperties['googleAppId'] : ''
        resValue 'string', 'google_api_key', firebaseProperties['googleApiKey']
                ? firebaseProperties['googleApiKey'] : ''
        resValue 'string', 'gcm_defaultSenderId', firebaseProperties['gcmDefaultSenderId']
                ? firebaseProperties['gcmDefaultSenderId'] : ''
        resValue 'string', 'project_id', firebaseProperties['projectId']
                ? firebaseProperties['projectId'] : ''
    }

    signingConfigs {
        betaRelease {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    productFlavors {
        prod {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            versionCode 1
            versionName '2025.1'
        }
        beta {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            applicationIdSuffix '.beta'
            versionCode 1
            versionName '2025.1-beta'
            buildTypes.release.signingConfig signingConfigs.betaRelease
        }
        dev {
            dimension 'default'
            resValue 'string', 'app_name', 'Money Manager'
            applicationIdSuffix '.dev'
            versionCode 1
            versionName '2025.1-dev'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (project.hasProperty('qa')) {
                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
}
